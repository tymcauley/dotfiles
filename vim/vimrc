" Load pathogen
filetype off
call pathogen#infect()
call pathogen#helptags()

" Attempt to determine the type of a file based on its name and possibly its
" contents. Use this to allow intelligent auto-indenting for each filetype,
" and for plugins that are filetype specific.
filetype plugin on
filetype indent on

" Enable syntax highlighting
let python_highlight_all=1
syntax on

" Set 'nocompatible' to ward off unexpected things that your distro might
" have made, as well as sanely reset options when re-sourcing .vimrc
set nocompatible

" Enable omni completion
set omnifunc=syntaxcomplete#Complete

" spelling (w.o. capitalization check)
set spellfile=~/.vim/dict.add
set spellcapcheck=''

" One of the most important options to activate. Allows you to switch from an
" unsaved buffer without saving it first. Also allows you to keep an undo
" history for multiple files. Vim will complain if you try to quit without
" saving, and swap files will keep you safe if your computer crashes.
set hidden

" Set command-line completion options.
set wildignore+=.svn,CVS,.git,*.pyc,*.o,*.a,*.class,*.mo,*.la,*.so,*.obj,*.swp,*.jpg,*.png,*.xpm,*.gif,*.pdf,*.bak,*.beam,*/tmp/*,*.zip,log/**,node_modules/**,target/**,tmp/**,*.rbc
set wildmode=longest,list,full

" Show partial commands in the last line of the screen
set showcmd

" Search settings.
set hlsearch      " highlight search
set incsearch     " reveal search incrementally as typed
set ignorecase    " case-insensitive match...
set smartcase     " ...except when uppercase letters are given

" Allow backspacing over autoindent, line breaks and start of insert action
set backspace=indent,eol,start

" When opening a new line and no filetype-specific indenting is enabled, keep
" the same indent as the line you're currently on. Useful for READMEs, etc.
set autoindent

" Always display the status line, even if only one window is displayed
set laststatus=2

" Instead of failing a command because of unsaved changes, instead raise a
" dialogue asking if you wish to save changed files.
set confirm

" Use visual bell instead of beeping when doing something wrong. No more
" annoying beeps!
set visualbell
set noerrorbells

" Enable use of the mouse for all modes
"set mouse=a

" By default, don't wrap text.
set nowrap

" Showing special characters:
"   Mark the end of a non-wrapped long line with a '#'.
"   Show non-breaking space characters as ¬.
"   Show tabs as ▸ followed by spaces.
set list
set listchars=extends:#,nbsp:¬,tab:▸\ ,

" When scrolling to the top or bottom of the screen, keep 2 lines between the
" cursor and the edge of the screen.
set scrolloff=2

" completion
set completeopt=longest,menuone

" Don't show what mode we're in on the last line. I'm already using airline.
set noshowmode

" Set the command window height to 2 lines, to avoid many cases of having to
" press <Enter> to continue"
set cmdheight=2

" Display line numbers on the left
set number

" Indentation settings for using hard tabs for indent. Display tabs as
" four characters wide.
set tabstop=4
set softtabstop=4
set shiftwidth=4
set expandtab

" Set the default location of window splits
set splitbelow
set splitright

" Prevent the quickfix/location buffer from showing up in typical buffer
" navigation commands.
augroup qf
    autocmd!
    autocmd FileType qf set nobuflisted
augroup END

" Default to using UTF8 encoding.
set encoding=utf-8

" Enable code folding
set foldmethod=indent
set foldlevel=99

" Flag trailing whitespace in certain filetypes.
"   Python
"   Verilog
"   SystemVerilog
"   Scala
highlight BadWhitespace ctermbg=red guibg=darkred
au BufRead,BufNewFile *.py,*.v,*.sv,*.scala match BadWhitespace /\s\+$/

" Wrap text after a certain number of characters for certain filetypes.
"   Python: 119 characters
"   Verilog: 119 characters
"   SystemVerilog: 119 characters
"   Scala: 119 characters
au BufRead,BufNewFile *.v,*.sv set textwidth=119
au BufRead,BufNewFile *.py set textwidth=119
au BufRead,BufNewFile *.scala set textwidth=119

" Add in a ruler for certain filetypes.
"   Python: Add a ruler 1 character past textwidth.
"   Verilog: Add a ruler 1 character past textwidth.
"   SystemVerilog: Add a ruler 1 character past textwidth.
"   Scala: Add a ruler 1 character past textwidth.
au BufRead,BufNewFile *.py,*.v,*.sv,*.scala set colorcolumn=+1
highlight ColorColumn ctermbg=0 guibg=lightgrey

" Intelligent comment formatting.
"   Python
"   Verilog
"   SystemVerilog
" Options:
"   r : Insert a new comment leader when inserting a newline in insert mode
"       (hitting <Enter>).
"   o : Insert a new comment leader when inserting a newline in normal mode
"       (using the 'o' or 'O' command).
"   c : Insert a new comment leader when wrapping text based on 'textwidth'.
au BufRead,BufNewFile *.py,*.v,*.sv set formatoptions+=r formatoptions+=o
    \ formatoptions+=c

" File-based indentation settings.
autocmd Filetype verilog,systemverilog setlocal ts=2 sts=2 sw=2 expandtab

"" Map settings

let mapleader=','       " map by , instead of /
let maplocalleader='\'  " map local by \

" clear search
nmap <silent> <leader>/ :nohlsearch<CR>

" spelling
nnoremap <silent> <leader>s :set spell!<CR>

" Delete trailing whitespace.
nnoremap <silent> <leader>w :call TrimWhitespace()<CR>

" Custom key mappings for commands I like.
set pastetoggle=<F2>  " Toggle paste mode for verbatim pasting.
" nnoremap <F7> :cd %:p:h<CR>:pwd<CR>
" nnoremap <F8> :set wrap!<CR>
" nnoremap <F9> :set paste!<CR>

" Split window navigation
nnoremap <C-h> <C-w>h
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k
nnoremap <C-l> <C-w>l

" Buffer navigation
map ga <C-^>
nmap gn :bn<CR>
nmap gp :bp<CR>
nmap gk :bp<bar>bd #<CR>

" complete by tab
" inoremap <silent> <expr> <tab> Smart_TabComplete()

" Enable code folding with the spacebar
nnoremap <space> za

" Function for removing trailing whitespace without affecting the cursor
" location/search history: http://vi.stackexchange.com/a/456
fun! TrimWhitespace()
  let l:save = winsaveview()
  %s/\s\+$//e
  call winrestview(l:save)
endfun

" smart tab complete from http://vim.wikia.com/wiki/Smart_mapping_for_tab_completion
function! Smart_TabComplete()
  let line = getline('.')                         " current line

  let substr = strpart(line, -1, col('.')+1)      " from the start of the current
                                                  " line to one character right
                                                  " of the cursor
  let substr = matchstr(substr, "[^ \t]*$")       " word till cursor
  if (strlen(substr)==0)                          " nothing to match on empty string
    return "\<tab>"
  endif
  let has_period = match(substr, '\.') != -1      " position of period, if any
  let has_slash = match(substr, '\/') != -1       " position of slash, if any
  if (!has_period && !has_slash)
    return "\<C-X>\<C-P>"                         " existing text matching
  elseif ( has_slash )
    return "\<C-X>\<C-F>"                         " file matching
  else
    return "\<C-X>\<C-O>"                         " plugin matching
  endif
endfunction

" status (airline)
set laststatus=2
if !exists('g:airline_symbols')
  let g:airline_symbols = {}
endif
let g:airline_theme = 'wombat'

let g:airline_left_sep = '▶'
let g:airline_right_sep = '◄'
let g:airline_symbols.linenr = '␤'
let g:airline_symbols.branch = '⎇'
let g:airline_symbols.paste = 'ρ'
let g:airline_symbols.whitespace = 'Ξ'

" file finding (ctrlp)
let g:ctrlp_map = '<leader>t'
map <c-p>  :CtrlP getcwd()<CR>
let g:ctrlp_user_command = 'ag %s -l -f --nocolor -g ""' " silver searcher
let g:ctrlp_switch_buffer = '' " always open a new instance for side-by-side

" Ctrl-P settings
" let g:ctrlp_map = '<C-f>'
" nnoremap <C-b> :CtrlPBuffer<CR>

" Font/color settings:
let base16colorspace=256  " Access colors present in 256 colorspace
colorscheme base16-harmonic16-dark

" Enable docstring previews for folded Python functions.
let g:SimpylFold_docstring_preview=1

" Syntastic settings
let g:syntastic_always_populate_loc_list = 1
let g:syntastic_auto_loc_list = 1
let g:syntastic_check_on_open = 1
let g:syntastic_check_on_wq = 0
let g:syntastic_python_python_exec = '/usr/bin/python3'  " Proper Python3 syntax checking

" Flake8 (Python syntax checker) settings
" let g:flake8_pyflake_marker=''     " Disable PyFlakes warnings
" let g:flake8_complexity_marker=''  " Disable McCabe complexity warnings
" let g:flake8_naming_marker=''      " Disable naming warnings
